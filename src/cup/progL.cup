/* ----------------------User code--------------------*/

package src;
import java_cup.runtime.*;
import java.util.Hashtable;

parser code {:

:};

action code {: 
    // Memory m  = Memory.getInstance();
    Command c = Command.getInstance();
:};
/* ------------Grammar symbol declarations----------- */
// Primitive set
terminal String         PRIMITIVE;
// TOKEN set
terminal                ASSIGNER, SEPARATOR, LPAREN, RPAREN;
// Operation set
terminal String         OPER_PLUS, OPER_MINUS, OPER_DIVIDE, OPER_MULTI;
// LEXEME set
terminal String         VARIABLE;
terminal Object         OBJECT;

non terminal PrimObj    type, expression;
non terminal            initial, declare, statement;

/* Precedences */
precedence left SEPARATOR, ASSIGNER;
precedence left OPER_PLUS, OPER_MINUS;
precedence left OPER_DIVIDE, OPER_MULTI;

/* ------------Grammar rules----------- */

start with initial;

initial ::= declare:d SEPARATOR           {:  :}
          | declare:d SEPARATOR statement:s SEPARATOR{:  :};

declare ::= declare:d1 SEPARATOR declare:d2         {:                   :}
          | type:p VARIABLE:v                       {: c.declare(v,p);   :}
          | type:p VARIABLE:v ASSIGNER OBJECT:o     {: c.declare(v,p,o); :}
          | type:p VARIABLE:v1 ASSIGNER VARIABLE:v2 {: PrimObj o = c.load(v2); c.declare(v1,p,o); :};

type ::= PRIMITIVE:p {: RESULT = PrimObj_Factory.get(p); :};

statement ::= statement:s1 SEPARATOR statement:s2   {: :}
          | VARIABLE:v ASSIGNER expression:e        {: c.assign(v,e); :};

expression ::= expression:e1 OPER_PLUS:oper   expression:e2  {: RESULT = c.plus(e1,e2); :}
             | expression:e1 OPER_MINUS:oper  expression:e2  {: RESULT = c.minus(e1,e2); :}
             | expression:e1 OPER_DIVIDE:oper expression:e2  {: RESULT = c.divide(e1,e2); :}
             | expression:e1 OPER_MULTI:oper  expression:e2  {: RESULT = c.multiply(e1,e2); :}
             | LPAREN expression:e RPAREN               {: RESULT = e; :}
             | OBJECT:o     {: RESULT = PrimObj_Factory.get(o); :}
             | VARIABLE:v   {: RESULT = c.load(v); :};
